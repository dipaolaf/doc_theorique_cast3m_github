************************************************************************
* Exemple de méthode d'optimisation topologique                        *
* Méthode SIMP + MMA (Method of Moving Asymptots - Svanberg)           *
* Application à une poutre en flexion                                  *
************************************************************************


** Paramètres globaux
OPTI 'DIME' 2 'MODE' 'PLAN' 'DEFO' 'ELEM' 'QUA4' 'ECHO' 0 'TRAC' 'PSC' ;
l      = 1. ;
h      = 0.2 ;
yo     = 200.E9 ;
nu     = 0.3 ;

** Maillage (controlé par sa taille de maille)
den1   = l / 120. ;
OPTI 'DENS' den1 ;
p1     = 0. 0. ;
p2     = l  0. ;
lb     = DROI p1 p2 ;
mail   = lb TRAN (0. h) ;
p3     = mail POIN 'PROC' ((l / 2.) h) ;
con    = CONT mail ;

** Modèle
mod    = MODE mail 'MECANIQUE' ;
ma0    = MATE mod 'YOUN' yo 'NU' nu ;

** Blocages
blo    = (BLOQ 'UY' p1) ET (BLOQ 'UX' 'UY' p2) ;

** Chargement
f      = FORC (0. -1.E5) p3 ;

** Paramètres d'optimisation
p      = 3. ;
fv     = 0.4 ;
eta    = 0.5 ;
rmin   = 1.5 * den1 ;
m      = 0.1 ;
xmin   = 0.001 ;
xmax   = 1. ;

** Optimisation topologique
* initialisation de la topologie
x      = MANU 'CHML' mod 'SCAL' fv 'GRAVITE' ;
* volume initial et cible
vini   = MESU mail ;
vcib   = vini * fv ;
* matrice de filtrage
un     = MANU 'CHML' mod 'SCAL' 1. 'GRAVITE' ;
vole   = INTG mod un 'ELEM' ;
mcg    = un POIN 'SUPERIEUR' 0. ;
wg     = PROI mcg vole ;
kfil   = MFIL wg rmin 1. 0. ;
* initialisation de la table pour la mma
nx = NBEL mail ;
tmma = TABL ;
* -- valeurs initiales de x
tmma . 'VX0' = TABL 'VECTEUR' ;
REPE bx nx ;
  tmma . 'VX0' . &bx = EXTR x 'SCAL' 1 &bx 1 ;
FIN bx ;
* -- bornes pour les valeurs de x
tmma . 'VXMIN' = TABL 'VECTEUR' ;
tmma . 'VXMAX' = TABL 'VECTEUR' ;
REPE bx nx ;
  tmma . 'VXMIN' . &bx = xmin ;
  tmma . 'VXMAX' . &bx = xmax ;
FIN bx ;
* -- indices pour la fonction objectif et la fonction contrainte
tmma . 'VF' = TABL 'VECTEUR' ;
tmma . 'MC' = TABL ;
tmma . 'MC' . 1 = TABL 'VECTEUR' ;
* -- bornes pour la fonction contrainte
tmma . 'VCMAX' = TABL 'VECTEUR' ;
tmma . 'VCMAX' . 1 = vcib ;
tmma . 'T0' = 0.1 ;
* boucle d'optimisation
liso  = PROG 0. 'PAS' 0.05 1. ;
REPE b1 100 ;
* pénalisation de la rigidité
  yop    = (x ** p) * yo ;
  map    = MATE mod 'YOUN' yop 'NU' nu ;
* résolution du problème mécanique
  rip    = RIGI mod map ;
  u      = RESO (rip ET blo) f ;
* déformée et tracé de la topologie
  def1   = DEFO mail u 1000. ;
  TRAC x mod con def1 liso 'TITR' (CHAI 'Iteration' ' ' (&b1 - 1)) 'NCLK' ;
* fonction objectif : compliance = uT.K.u = Int(sig:eps)dV
  eps    = EPSI 'LINE' mod u ;
  sig    = ELAS mod map eps ;
  psi    = INTG mod (ENER mod sig eps) ;
* sensibilité
  sig0   = ELAS mod ma0 eps ;
  ene0   = ENER mod eps sig0 ;
  ene    = CHAN ene0 mod 'GRAVITE' ;
  dpsi   = (-1. * p * (x ** (p - 1.)) * ene) ;
* filtrage de la sensibilité
  xdpsi  = x * dpsi ;
  xdpsi0 = PROI mcg xdpsi ;
  xdpsi1 = kfil * xdpsi0 ;
  xdpsi  = MANU 'CHML' mod 'REPA' 'SCAL' (EXTR xdpsi1 'VALE') 'TYPE' 'SCALAIRE' 'GRAVITE' ;
  dpsi   = xdpsi / x ;
* optimisation d'une nouvelle topologie (méthode des asymptotes mobiles)
* valeur de la fonction objectif et valeurs de ses sensibilités
  tmma . 'VF' . 0 = psi ;
  REPE bx nx ;
    tmma . 'VF' . &bx = EXTR dpsi 'SCAL' 1 &bx 1 ;
  FIN bx ;
* valeurs de la fonction contrainte (le volume) et de ses sensibilités
  tmma . 'MC' . 1 . 0 = INTG mod x ;
  REPE bx nx ;
    tmma . 'MC' . 1 . &bx = EXTR vole 'SCAL' 1 &bx 1 ;
  FIN bx ;
* calcul d'un nouveau x
  tmma_new = EXCE tmma ;
  lxnew = PROG ;
  REPE b2 nx ;
    lxnew = lxnew ET (tmma_new . 'VX0' . &b2) ;
  FIN b2 ;
  xnew  = MANU 'CHML' mod 'REPA' 'SCAL' lxnew 'TYPE' 'SCALAIRE' 'GRAVITE' ;
  xnew = REDU xnew mod ;
* bilan de l'itération
  vnew   = INTG mod xnew ;
  change = MAXI 'ABS' (x - xnew) ;
  MESS 'It.' ' ' &b1 '   Obj.' ' ' psi '   Change' ' ' change '   Fvol.' ' ' (vnew / vini) ;
  SI (change < 0.01) ;
    QUIT b1 ;
  FINSI ;
* préparation de la nouvelle itération
  x      = xnew ;
  tmma . 'VX0' = tmma_new . 'VX0' ;
FIN  b1 ;

** Tracé de la topologie finale
yop    = (xnew ** p) * yo ;
map    = MATE mod 'YOUN' yop 'NU' nu ;
rip    = RIGI mod map ;
u      = RESO (rip ET blo) f ;
def1   = DEFO mail u 1000. ;
TRAC x mod con def1 liso 'TITR' (CHAI 'Iteration' ' ' (&b1 - 1)) ;


FIN ;
