************************************************************************
*      Cas test elementaire pour la loi d'endommagement de Mazars      *
*           Essai de compression sous confinement (triaxial)           *
*                                                                      *
* Les donnees experimentales servant de reference sont issues de :     *
* "Study of the Behavior of Concrete under Triaxial Compression"       *
* Domingo Sfer, Ignacio Carol, Ravindra Gettu, Guillermo Etse          *
* Journal of Engineering Mechanics, 2002, 128(2): 156-163              *
* doi : 10.1061/(ASCE)0733-9399(2002)128:2(156)                        *
*                                                                      *
* Le test est realise avec plusieurs modes de calcul :                 *
*   1) 3D volumique                                                    *
************************************************************************

** Options pour les traces
OPTI 'TRAC' 'PSC' ;

** Liste des modes de calcul a traiter
lcas = LECT 1 ;

** Parametres geometriques
rayo = 75.E-3 ;
haut = 300.E-3 ;

** Parametres materiau
you   = 28.E9 ;
nu    = 0.2 ;
ft    = 2.1E6 ;
aldi  = 9.E-3 ;
gam1  = 3.E9 ;
a1    = 8.E-6 ;
sigf  = -3.E6 ;
fc    = 10.E6 ;
af    = 1. ;
ag    = 1. ;
ac    = 4.E10 ;
bc    = 600. ;
sigu  = -6.E6 ;
hyst  = 1. ;

** Parametres chargement
lpres  = PROG 0. 1.5E6 4.5E6 9.E6 ;
uzimp  = 0.015 * haut ;



**************** C A S  #1  :  3 D   V O L U M I Q U E ****************
SI (EXIS lcas 1) ;

** Options generales
  OPTI 'DIME' 3 'MODE' 'TRID' 'ELEM' 'CUB8' ;

** Maillage
  long    = rayo * (pi ** 0.5) ;
  p1      = 0. 0. 0. ;
  p2      = 0. long 0. ;
  l12     = DROI 1 p1 p2 ;
  sgauche = l12 TRAN 1 (0. 0. haut) ;
  mail    = sgauche VOLU 'TRAN' 1 (long 0. 0.) ;
  sdroite = mail FACE 2 ;
  env1    = ENVE mail ;
  sbas    = env1 ELEM 5 ;
  shaut   = env1 ELEM 2 ;
  sdevant = env1 ELEM 4 ;
  sderrie = env1 ELEM 3 ;
  p3      = mail POIN 'PROC' (0. 0. haut) ;
  mes1    = MESU mail ;

** Modele et caracteristiques materiau
  mo = MODE mail 'MECANIQUE' 'ELASTIQUE' 'ENDOMMAGEMENT' 'RICBET' ;
  ma = MATE mo 'YOUN' you 'NU' nu 'FT' ft 'ALDI' aldi 'GAM1' gam1
                  'A1' a1 'SREF' sigf 'FC' fc 'AF' af 'AG' ag 'AC' ac
                  'BC' bc 'SIGU' sigu ;

** Blocages
  blhaut = BLOQ 'UZ' shaut ;
  blrig  = (BLOQ 'UX' sgauche) ET (BLOQ 'UY' sdevant) ET (BLOQ 'UZ' sbas) ;
  bl     = blhaut ET blrig ;

** Chargements (plusieurs pressions imposees, deplacement impose)
  evc1   = EVOL 'MANU' (PROG 0. 0.5 1.) (PROG 0. 1. 1.) ;
  lcoul  = MOTS 'BLEU' 'ORAN' 'VERT' 'ROUG' ;
  evtot0 = VIDE 'EVOLUTIO' ;
  evtot1 = VIDE 'EVOLUTIO' ;
  evtot2 = VIDE 'EVOLUTIO' ;
  tleg   = TABL ;
  tleg . 'TITRE' = TABL ;
  npres = DIME lpres ;
  REPE b0 npres ;
* On fait un premier calcul avec seulement la pression imposee
    pre1 = EXTR lpres &b0 ;
    fpr  = PRES 'MASS' mo (sdroite ET sderrie ET shaut) pre1 ;
    chap = CHAR 'MECA' fpr evc1 ;
* Resolution 1 : mise en pression seulement
    t                               = TABL ;
    t . 'MODELE'                    = mo ;
    t . 'CARACTERISTIQUES'          = ma ;
    t . 'BLOCAGES_MECANIQUES'       = blrig ;
    t . 'CHARGEMENT'                = chap ;
    t . 'TEMPS_CALCULES'            = PROG 0. 'PAS' 0.1 0.5 ;
    t . 'MOVA'                      = MOT 'D' ;
    t . 'MES_SAUVEGARDES'           = TABL ;
    t . 'MES_SAUVEGARDES' . 'DEFTO' = VRAI ;
    PASAPAS t ;
* Resolution 2 : pression + ajout du deplacement impose
    dihaut = DEPI blhaut 1. ;
    uz0    = EXTR (t . 'DEPLACEMENTS' . 5) 'UZ' p3 ;
    evc2   = EVOL 'MANU' (PROG 0.5 1.) (PROG uz0 (uz0 - uzimp)) ;
    chadi  = CHAR 'DIMP' dihaut evc2 ;
    t . 'BLOCAGES_MECANIQUES'       = bl ;
    t . 'CHARGEMENT'                = chap ET chadi ;
    t . 'TEMPS_CALCULES'            = PROG 0.505 'PAS' 0.005 1. ;
    PASAPAS t ;
*    tit1 = CHAI '[3D V] UZ vs Temps - P =' ' ' pre1 ;
*    DESS (EVOL 'TEMPE' t 'DEPLACEMENTS' 'UZ' p3) 'TITR' tit1 ;
*    tit1 = CHAI '[3D V] SMZZ vs Temps - P =' ' ' pre1 ;
*    DESS (EVOL 'TEMP' t 'CONTRAINTES' 'SMZZ' 1 1 1) 'TITR' tit1 ;

** Post traitement :
* courbes : endommagement moyen vs temps
*           contrainte moyenne vs deformation moyenne
*           force de reaction vs deplacement impose
    tt = t . 'TEMPS' ;
    tc = t . 'CONTRAINTES' ;
    te = t . 'DEFORMATIONS' ;
    tv = t . 'VARIABLES_INTERNES' ;
    tu = t . 'DEPLACEMENTS' ;
    tr = t . 'REACTIONS' ;
    ltps = PROG ;
    lend = PROG ;
    lsig = PROG ;
    leps = PROG ;
    ldep = PROG ;
    lrea = PROG ;
    REPE b1 (DIME tt) ;
      tps1 = tt . (&b1 - 1) ;
      end1 = (INTG mo (tv . (&b1 - 1)) 'D')    / mes1 ;
      sig1 = (INTG mo (tc . (&b1 - 1)) 'SMZZ') / mes1 ;
      eps1 = (INTG mo (te . (&b1 - 1)) 'EPZZ') / mes1 ;
      u1   = EXTR (tu . (&b1 - 1)) 'UZ' p3 ;
      rea1 = 0. ;
      SI ((NBEL (EXTR (tr . (&b1 - 1)) 'MAIL')) > 0) ;
        rea1 = @TOTAL (tr . (&b1 - 1)) sbas 'FZ' ;
      FINSI ;
      ltps = ltps ET tps1 ;
      lend = lend ET end1 ;
      lsig = lsig ET sig1 ;
      leps = leps ET eps1 ;
      ldep = ldep ET u1 ;
      lrea = lrea ET rea1 ;
    FIN b1 ;
    evtot0 = evtot0 ET (EVOL (EXTR lcoul &b0) 'MANU' 'Temps' ltps 'D'    lend) ;
    evtot1 = evtot1 ET (EVOL (EXTR lcoul &b0) 'MANU' 'EPZZ'  leps 'SMZZ' lsig) ;
    evtot2 = evtot2 ET (EVOL (EXTR lcoul &b0) 'MANU' 'UZ'    ldep 'FZ'   lrea) ;
    tleg . 'TITRE' . &b0 = CHAI 'P =' ' ' pre1 ;
  FIN b0 ;

** Donnees experimentales de reference
  evxp = VIDE 'EVOLUTIO' ;
  tleg . (npres + 1) = MOT 'MARQ CROI NOLI' ;
  tleg . 'TITRE' . (npres + 1) = 'Exp. P = 0 MPa' ;
  txp  = LIRE 'CSV' '09_triaxial_0MPa_courbe0.csv' 'DEBU' 2 'SEPA' ',' ;
  evxp = evxp ET (EVOL 'BLEU' 'MANU' 'EPZZ' (txp . 1) 'SMZZ' (txp . 2)) ;
  tleg . (npres + 2) = MOT 'MARQ CROI NOLI' ;
  tleg . 'TITRE' . (npres + 2) = 'Exp. P = 1.5 MPa' ;
  txp  = LIRE 'CSV' '09_triaxial_1.5MPa_courbe0.csv' 'DEBU' 2 'SEPA' ',' ;
  evxp = evxp ET (EVOL 'ORAN' 'MANU' 'EPZZ' (txp . 1) 'SMZZ' (txp . 2)) ;
  tleg . (npres + 3) = MOT 'MARQ CROI NOLI' ;
  tleg . 'TITRE' . (npres + 3) = 'Exp. P = 4.5 MPa' ;
  txp  = LIRE 'CSV' '09_triaxial_4.5MPa_courbe0.csv' 'DEBU' 2 'SEPA' ',' ;
  evxp = evxp ET (EVOL 'VERT' 'MANU' 'EPZZ' (txp . 1) 'SMZZ' (txp . 2)) ;
  tleg . (npres + 4) = MOT 'MARQ CROI NOLI' ;
  tleg . 'TITRE' . (npres + 4) = 'Exp. P = 9 MPa' ;
  txp  = LIRE 'CSV' '09_triaxial_9MPa_courbe0.csv' 'DEBU' 2 'SEPA' ',' ;
  evxp = evxp ET (EVOL 'ROUG' 'MANU' 'EPZZ' (txp . 1) 'SMZZ' (txp . 2)) ;

** Trace des courbes pour les differents niveaux de pression
  DESS evtot0 'TITR' '[3D V] Endommagement vs Temps'    'LEGE' tleg ;
  DESS (evtot1 ET evxp) 'TITR' '[3D V] Contrainte vs Deformation' 'LEGE' tleg ;
  DESS evtot2 'TITR' '[3D V] Reaction vs Deplacement'   'LEGE' tleg ;
FINSI ;




FIN ;
